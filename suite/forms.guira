let macro
  form [name args . exprs]
    quote
      let [unquote name]
        form [unquote args]
          unquote
            pair [quote begin]
                 exprs

!macro fun[name args . exprs]
  quote
    let [unquote name]
      function [unquote args]
        unquote
          pair [quote begin]
               exprs

!fun square x
  * x x

!fun fact[n]
  if [<= n 1]
     1
     [* n [fact [- n 1]]]

!fun sum[args]
  if [= args nil]
     0
     + [head args] [sum [tail args]]

if [not= [square 2] 4]
  abort "fail 1"
  nil
if [not= [fact 6] 720]
  abort "fail 2"
  nil
if [not= [sum [list 1 2 3 4 5 6]] 21]
  abort "fail 3"
  nil

print "forms.guira: OK"
